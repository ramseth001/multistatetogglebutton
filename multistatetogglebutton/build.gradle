plugins {
    id 'com.android.library'
    id 'com.jfrog.bintray'
    id 'maven-publish'
}

def mVersionCode = 10
def mVersionName = "0.2.2"
group = "org.honorato.multistatetogglebutton"
version = mVersionName

def siteUrl = 'https://github.com/jlhonora/MultiStateToggleButton'
def gitUrl = 'https://github.com/jlhonora/MultiStateToggleButton.git'
def gitSSHUrl = 'git@github.com:jlhonora/MultiStateToggleButton.git'

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 31
    }

    buildTypes {
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }
    }
}

Properties properties = new Properties()
File file = project.rootProject.file('local.properties')
if (file.isFile()) {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "MultiStateToggleButton"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        issueTrackerUrl = siteUrl + '/issues'
        licenses = ["MIT"]
        publish = true
        publicDownloadNumbers = true
    }
}

dependencies {
    api 'androidx.appcompat:appcompat:1.4.0'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

task findConventions {
    doLast {
        println project.getConvention()
    }
}

project.afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
            }
        }
    }
}